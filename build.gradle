plugins {
    id "com.github.jruby-gradle.base" version "1.5.0"
    id "com.github.johnrengelman.shadow" version "6.1.0" apply false
    id "java"
    id "checkstyle"
    id "com.palantir.git-version" version "0.12.3"
    id "com.diffplug.spotless" version "6.13.0"
}
import com.github.jrubygradle.JRubyExec
repositories {
    mavenCentral()
    jcenter()
}

version = {
    def vd = versionDetails()
    if (vd.commitDistance == 0 && vd.lastTag ==~ /^[0-9]+\.[0-9]+\.[0-9]$/) {
        vd.lastTag
    } else {
        "0.0.0.${vd.gitHash}"
    }
}()


sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compileOnly "org.embulk:embulk-core:0.9.23"
    implementation "com.google.code.externalsortinginjava:externalsortinginjava:0.6.2"
    implementation project(path: ":shadow-kintone-java-client", configuration: "shadow")

    testImplementation "junit:junit:4.+"
    testImplementation "org.embulk:embulk-test:0.9.23"
    testImplementation "org.embulk:embulk-standards:0.9.23"
    testImplementation "org.embulk:embulk-deps-buffer:0.9.23"
    testImplementation "org.embulk:embulk-deps-config:0.9.23"
    testImplementation "org.mockito:mockito-inline:4.11.0"
    testImplementation "ch.qos.logback:logback-classic:1.3.8"
    testImplementation "net.jcip:jcip-annotations:1.0"
}

task classpath(type: Copy, dependsOn: ["jar"]) {
    doFirst { file("classpath").deleteDir() }
    from (configurations.runtimeClasspath + files(jar.archivePath))
    into "classpath"
}
clean { delete "classpath" }

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

task gem(type: JRubyExec, dependsOn: ["gemspec", "classpath"]) {
    jrubyArgs "-S"
    script "gem"
    scriptArgs "build", "${project.name}.gemspec"
    doLast { ant.move(file: "${project.name}-${project.version}.gem", todir: "pkg") }
}

task gemPush(type: JRubyExec, dependsOn: ["gem"]) {
    jrubyArgs "-S"
    script "gem"
    scriptArgs "push", "pkg/${project.name}-${project.version}.gem"
}

task "package"(dependsOn: ["gemspec", "classpath"]) {
    doLast {
        println "> Build succeeded."
        println "> You can run embulk with '-L ${file(".").absolutePath}' argument."
    }
}

task gemspec {
    ext.gemspecFile = file("${project.name}.gemspec")
    inputs.file "build.gradle"
    outputs.file gemspecFile
    doLast { gemspecFile.write($/
Gem::Specification.new do |spec|
  spec.name          = "${project.name}"
  spec.version       = "${project.version}"
  spec.authors       = ["takeshi fujita"]
  spec.summary       = %[kintone output plugin for Embulk]
  spec.description   = %[Dumps records to Kintone.]
  spec.email         = ["tksfjt1024@gmail.com"]
  spec.licenses      = ["MIT"]
  spec.homepage      = "https://github.com/trocco-io/embulk-output-kintone"

  spec.files         = `git ls-files`.split("\n") + Dir["classpath/*.jar"]
  spec.test_files    = spec.files.grep(%r"^(test|spec)/")
  spec.require_paths = ["lib"]

  #spec.add_dependency 'YOUR_GEM_DEPENDENCY', ['~> YOUR_GEM_DEPENDENCY_VERSION']
  spec.add_development_dependency 'bundler', ['~> 1.0']
  spec.add_development_dependency 'rake', ['~> 12.0']
end
/$)
    }
}
clean { delete "${project.name}.gemspec" }

spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
        formatAnnotations()
        toggleOffOn()
    }
}

test {
    forkEvery = 1
    maxHeapSize = "1g"
}
