plugins {
    id "java"
    id "org.embulk.embulk-plugins" version "0.7.0"
    id "com.palantir.git-version" version "3.1.0"
    id "com.diffplug.spotless" version "6.13.0" // Downgrade to match Java 8
}

repositories {
    mavenCentral()
}

group = "trocco-io"
version = {
    def vd = versionDetails()
    if (vd.commitDistance == 0 && vd.lastTag ==~ /^[0-9]+\.[0-9]+\.[0-9]+$/) {
        vd.lastTag
    } else {
        "0.0.0.${vd.gitHash}"
    }
}()
description = "Dumps records to Kintone."

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

configurations {
    compileClasspath.resolutionStrategy.activateDependencyLocking()
    runtimeClasspath.resolutionStrategy.activateDependencyLocking()
}

dependencies {
    compileOnly "org.embulk:embulk-spi:0.11"

    implementation "org.embulk:embulk-util-config:0.3.4" // Downgrade for compatibility with Embulk v0.9
    implementation "org.embulk:embulk-util-json:0.3.0" // Downgrade for compatibility with Embulk v0.9
    implementation "org.embulk:embulk-util-retryhelper:0.9.0"
    implementation "org.apache.commons:commons-lang3:3.4" // Downgrade to match Embulk runtime
    implementation "com.google.guava:guava:18.0" // Downgrade to match Embulk runtime
    implementation "com.google.code.externalsortinginjava:externalsortinginjava:0.6.2"
//  implementation "com.kintone:kintone-java-client:2.3.0"
    implementation project(path: ":shadow", configuration: "shadow")

    testImplementation "org.embulk:embulk-spi:0.11"
    testImplementation "org.embulk:embulk-deps:0.11.5"
    testImplementation "org.embulk:embulk-input-file:0.11.0" // Downgrade to match the version of embulk-util-config
    testImplementation "org.embulk:embulk-parser-csv:0.11.4" // Downgrade to match the version of embulk-util-config & embulk-util-json
    testImplementation "org.embulk:embulk-junit4:0.11.5"
    testImplementation "org.mockito:mockito-inline:4.11.0" // Downgrade to match Java 8
    testImplementation "ch.qos.logback:logback-classic:1.3.15" // Downgrade to match Java 8
    testImplementation "net.jcip:jcip-annotations:1.0"
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "-Xlint:all" << "-Xlint:-serial"
}

embulkPlugin {
    mainClass = "org.embulk.output.kintone.KintoneOutputPlugin"
    category = "output"
    type = "kintone"
}

gem {
    authors = [ "takeshi fujita" ]
    email = [ "tksfjt1024@gmail.com" ]
    summary = "kintone output plugin for Embulk"
    homepage = "https://github.com/trocco-io/embulk-output-kintone"
    licenses = [ "MIT" ]
    from("LICENSE.txt")
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
        formatAnnotations()
        toggleOffOn()
    }
}

test {
    forkEvery = 1
    maxHeapSize = "1g"
}
